// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import "c";
import "c/libc";
import "runtime";

import { Builder } from "../strings";

public alias id = runtime.process_id;
public alias panic = runtime.process_panic;
public alias abort = runtime.process_abort;
public alias exit = runtime.process_exit;
public alias executable = runtime.process_executable;
public alias set_cwd = runtime.process_set_cwd;
public alias get_cwd = runtime.process_get_cwd;

public let ARGS: []string = unsafe { runtime.ARGS };
public let WD_AT_STARTUP: string = get_cwd() catch ".";

struct Result {
	public output: string;
	public exit_code: i32;
}

/// Starts the specified command, waits for it to complete, and returns
/// both its output and the exit code.
public func execute(cmd: string) Result {
    let pcmd = if cmd.contains("2>") { cmd } else { "{} 2>&1".fmt(cmd) };
    unsafe {
        if let f = libc.popen(pcmd.ptr, c"r") {
            let fd = libc.fileno(f);
            let mut buf: [4096]mut u8 = []!;
            let pbuf: *mut u8 = &mut buf[0];
            let mut output = Builder.new(1024);
            while {
                let len = libc.read(fd, pbuf, 4096);
                if len == 0 or len == -1 {
                    break;
                }
                output.write_raw_with_len(pbuf, @as(usize, len));
            }
            return Result(output.to_string(), libc.pclose(f));
        }
        return Result("execute( {} ) failed".fmt(cmd), -1);
    }
}
