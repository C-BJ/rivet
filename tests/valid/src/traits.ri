trait Speaker {
    func speak(&self) string;
}

class Animal2 : Speaker {
    func speak(self) string {
        return "~ Animal2.speak() ~";
    }
}

class Frog : Animal2 {
    #[override]
    func speak(self) string {
        return "Croack";
    }
}

class Lion : Animal2 { }

func speaker(s: Speaker) string {
    return s.speak();
}

test "traits: calling class method" {
    @assert(speaker(Animal2()) == "~ Animal2.speak() ~");
}

test "traits: calling inherited method" {
    @assert(speaker(Lion()) == "~ Animal2.speak() ~");
}

test "traits: calling overrided method" {
    @assert(speaker(Frog()) == "Croack");
}

trait Reader {
    func read(&self) u8;
}

trait Writer {
    func write(&self, b: u8) bool;
}

trait ReaderWriter : Reader, Writer {
    func write_and_read(&self, b: u8) u8;
}

struct SomeStream : ReaderWriter {
    func read(&self) u8 {
        return b'A';
    }

    func write(&self, b: u8) bool { return true; }

    func write_and_read(&self, b: u8) u8 {
        _ = self.write(b);
        return self.read();
    }
}

func writer(w: Writer) bool {
    return w.write(b'A');
}

func reader(r: Reader) u8 {
    return r.read();
}

func reader_writer(rw: ReaderWriter) u8 {
    return rw.write_and_read(b'A');
}

test "traits: inheritance" {
    let ss = SomeStream();
    @assert(writer(ss));
    @assert(reader(ss) == b'A');
    @assert(reader_writer(ss) == b'A');
}
